buildscript {
    repositories {
        maven { url = 'https://repo.spongepowered.org/maven' }
    }
    dependencies {
        classpath 'org.spongepowered:mixingradle:0.7.+'
    }
}

plugins {
    id 'eclipse'
    id 'maven-publish'
    id 'net.minecraftforge.gradle' version '5.1.+' apply false
}

subprojects {

    apply plugin: 'net.minecraftforge.gradle'
    apply plugin: 'org.spongepowered.mixin'

    println "Setup: ${project.name}"

    version = "${mc_version}-${mod_version}"
    group = 'fr.modcraftmc.datasync'
    archivesBaseName = "${project.name}"

    java.toolchain.languageVersion = JavaLanguageVersion.of(17)

    repositories {
        maven { url = 'https://maven.modcraftmc.fr/releases' }
        maven { url "https://cursemaven.com" }
    }

    dependencies {
        minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"
        annotationProcessor 'org.spongepowered:mixin:0.8.5:processor'
        implementation('fr.modcraftmc:cross-server-core:1.1.8')
    }

    minecraft {
        mappings channel: 'official', version: '1.19.2'
        accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')
    }

    mixin {
        add sourceSets.main, "${project.name}.refmap.json"
        config "${project.name}.mixin.json"
    }

    sourceSets.main.resources { srcDir 'src/generated/resources' }

    jar {
        manifest {
            attributes([
                    "Specification-Title"     : "${project.name}",
                    "Specification-Vendor"    : "ModcraftMC",
                    "Specification-Version"   : "${mod_version}",
                    "Implementation-Title"    : project.name,
                    "Implementation-Version"  : project.jar.archiveVersion,
                    "Implementation-Vendor"   : "ModcraftMC",
                    "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                    "TweakClass"              : "org.spongepowered.asm.launch.MixinTweaker",
                    "TweakOrder"              : "0",
                    "MixinConfigs"            : "${project.name}.mixin.json"
            ])
        }

        //rename 'mixin.refmap.json', 'datasync.mixin-refmap.json'
    }

    tasks.register('copyJars', Copy) {
        from jar
        into "../builds"
    }
    build.finalizedBy copyJars
}